<html>
    <head>
        <title></title>
    </head>
    <body>
<h1>How to Use the Microsoft® Smooth Streaming Plugin for the Adobe Open Source Media Framework</h1>

<h2>Overview</h2>

<p>The Microsoft® Smooth Streaming plugin for Open Source Media Framework 2.0 (SS for OSMF) extends the default capabilities of OSMF and adds Microsoft® Smooth Streaming content playback to new and existing OSMF players. The plugin also adds Smooth Streaming playback capabilities to Strobe Media Playback (SMP).</p>

<p>SS for OSMF includes two versions of plugin:</p>

<ul>
<li><p>Static Smooth Streaming plugin for OSMF (.swc)</p></li>
<li><p>Dynamic Smooth Streaming plugin for OSMF (.swf)</p></li>
</ul>

<p>This document assumes that the reader has a general working knowledge of OSMF and OSMF plug-ins. For more information about OSMF, please see the documentation on the <a href="http://www.osmf.org/">official OSMF site</a>.</p>

<h3>Smooth Streaming plugin for OSMF 2.0</h3>

<p>The plugin supports loading and playback of on-demand Smooth Streaming content with the following features:</p>

<ul>
<li>• On-demand Smooth Streaming playback (Play, Pause, Seek, Stop)</li>
<li>Support for video codecs – H.264</li>
<li>Support for Audio codecs – AAC</li>
<li>Multiple audio language switching with OSMF built-in APIs</li>
<li>Max playback quality selection with OSMF built-in APIs</li>
<li>Sidecar closed captions with OSMF captions plugin</li>
<li>Adobe® Flash® Player 10.2 or higher.</li>
<li>This version only supports OSMF 2.0.</li>
</ul>

<p>The following are unsupported features:</p>

<ul>
<li>Live Smooth Streaming playback</li>
<li>VC-1 and WMA codec</li>
<li>Content protection (PlayReady)</li>
<li>Text and Sparse Tracks</li>
<li>Trickplay (slow motion, fast-forward, and rewind)</li>
</ul>

<p>The following is a list of known issues:</p>

<ul>
<li>Playback of Smooth Streaming content with 48KHz audio tracks have issues. Flash runtime have a known issue for rendering 48KHz audio content. Because of this issue, Smooth Streaming content encoded with 48Khz settings might not work as expected. Please see: <a href="http://forums.adobe.com/message/4483498#4483498">Using Flash Player</a> and <a href="https://bugbase.adobe.com/index.cfm?event=bug&amp;id=3210964">Adobe Flash Player 11.3  -  Bug 3210964</a> for more info.</li>
<li>Multiple Smooth Streaming content playback on single page might cause issues. This is a known issue with OSMF.</li>
<li>Playback of Stage video might cause problems and no video on some machines. As a workaround you can disable hardware acceleration or Stage video.</li>
</ul>

<h2>Loading the Plugin</h2>

<p>OSMF plugins can be loaded statically (at compile time) or dynamically (at run-time). The Smooth Streaming plugin for OSMF download includes both dynamic and static versions.</p>

<ul>
<li><p>Static loading: To load statically, a static library (SWC) file is required. Static plugins are added as a reference to the projects and merge inside the final output file at the compile time.</p></li>
<li><p>Dynamic loading: To load dynamically, a precompiled (SWF) file is required. Dynamic plugins are loaded in the runtime and not included in the project output. (Compiled output) Dynamic plugins can be loaded using HTTP and FILE protocols.</p></li>
</ul>

<p>For more information on static and dynamic loading, see the official <a href="http://osmf.org/dev/osmf/OtherPDFs/osmf_plugin_dev_guide.pdf">OSMF plugin page</a>.</p>

<p><strong>Note</strong> The following code samples are based on pre-release (Beta) builds of software and components; therefore, some features might not be available in future releases and are subject to change without notification.</p>

<h3>SS for OSMF Static Loading</h3>

<p>The code snippet below shows how to load the SS plugin for OSMF statically and play a basic video using OSMF MediaFactory class. Before including the SS for OSMF code, please ensure that the project reference includes the “MSAdaptiveStreamingPlugin-v1.0.1-osmf2.0.swc” static plugin.</p>

<pre><code>
package 
{

    import com.microsoft.azure.media.AdaptiveStreamingPluginInfo;

    import flash.display.*;
    import org.osmf.media.*;
    import org.osmf.containers.MediaContainer;
    import org.osmf.events.MediaErrorEvent;
    import org.osmf.events.MediaFactoryEvent;
    import org.osmf.events.MediaPlayerStateChangeEvent;
    import org.osmf.layout.*;



    [SWF(width="1024", height="768", backgroundColor='#405050', frameRate="25")]
    public class TestPlayer extends Sprite
    {        
        public var _container:MediaContainer;
        public var _mediaFactory:DefaultMediaFactory;
        private var _mediaPlayerSprite:MediaPlayerSprite;


        public function TestPlayer( )
        {
            stage.quality = StageQuality.HIGH;

            initMediaPlayer();

        }

        private function initMediaPlayer():void
        {

            // Create the container (sprite) for managing display and layout
            _mediaPlayerSprite = new MediaPlayerSprite();    
            _mediaPlayerSprite.addEventListener(MediaErrorEvent.MEDIA_ERROR, onPlayerFailed);
            _mediaPlayerSprite.addEventListener(MediaPlayerStateChangeEvent.MEDIA_PLAYER_STATE_CHANGE, onPlayerStateChange);
            _mediaPlayerSprite.scaleMode = ScaleMode.NONE;
            _mediaPlayerSprite.width = stage.stageWidth;
            _mediaPlayerSprite.height = stage.stageHeight;
            //Adds the container to the stage
            addChild(_mediaPlayerSprite);

            // Create a mediafactory instance
            _mediaFactory = new DefaultMediaFactory();

            // Add the listeners for PLUGIN_LOADING
            _mediaFactory.addEventListener(MediaFactoryEvent.PLUGIN_LOAD,onPluginLoaded);
            _mediaFactory.addEventListener(MediaFactoryEvent.PLUGIN_LOAD_ERROR, onPluginLoadFailed );

            // Load the plugin class 
            loadAdaptiveStreamingPlugin( );  

        }

        private function loadAdaptiveStreamingPlugin( ):void
        {
            var pluginResource:MediaResourceBase;

            pluginResource = new PluginInfoResource(new AdaptiveStreamingPluginInfo( )); 
            _mediaFactory.loadPlugin( pluginResource ); 
        }

        private function onPluginLoaded( event:MediaFactoryEvent ):void
        {
            // The plugin is loaded successfully.
            // Your web server needs to host a valid crossdomain.xml file to allow plugin to download Smooth Streaming files.
        loadMediaSource("http://devplatem.vo.msecnd.net/Sintel/Sintel_H264.ism/manifest")

        }

        private function onPluginLoadFailed( event:MediaFactoryEvent ):void
        {
            // The plugin is failed to load ...
        }


        private function onPlayerStateChange(event:MediaPlayerStateChangeEvent) : void
        {
            var state:String;

            state =  event.state;

            switch (state)
            {
                case MediaPlayerState.LOADING: 

                    // A new source is started to load.

                    break;

                case  MediaPlayerState.READY :   
                    // Add code to deal with Player Ready when it is hit the first load after a source is loaded. 

                    break;

                case MediaPlayerState.BUFFERING :

                    break;

                case  MediaPlayerState.PAUSED :
                    break;      
                // other states ...          
            }
        }

        private function onPlayerFailed(event:MediaErrorEvent) : void
        {
            // Media Player is failed .           
        }

        private function loadMediaSource(sourceURL : String):void 
        {
            // Take an URL of SmoothStreamingSource’s manifest and add it to the page.

            var resource:URLResource= new URLResource( sourceURL );

            var element:MediaElement = _mediaFactory.createMediaElement( resource );
            _mediaPlayerSprite.scaleMode = ScaleMode.LETTERBOX;
            _mediaPlayerSprite.width = stage.stageWidth;
            _mediaPlayerSprite.height = stage.stageHeight;

            // Add the media element
            _mediaPlayerSprite.media = element;
        }     

    }
}
</code></pre>

<h3>SS for OSMF Dynamic Loading</h3>

<p>The code snippet below shows how to load the SS plugin for OSMF dynamically and play a basic video using the OSMF MediaFactory class. Before including the SS for OSMF code, copy the “MSAdaptiveStreamingPlugin-v1.0.1-osmf2.0.swf” dynamic plugin to the project folder if you want to load using FILE protocol, or copy under a web server for HTTP load. There is no need to include “MSAdaptiveStreamingPlugin-v1.0.1-osmf2.0.swc” in the project references.</p>

<pre><code>
package 
{

    import flash.display.*;
    import org.osmf.media.*;
    import org.osmf.containers.MediaContainer;
    import org.osmf.events.MediaErrorEvent;
    import org.osmf.events.MediaFactoryEvent;
    import org.osmf.events.MediaPlayerStateChangeEvent;
    import org.osmf.layout.*;
    import flash.events.Event;
    import flash.system.Capabilities;


    //Sets the size of the SWF

    [SWF(width="1024", height="768", backgroundColor='#405050', frameRate="25")]
    public class TestPlayer extends Sprite
    {        
        public var _container:MediaContainer;
        public var _mediaFactory:DefaultMediaFactory;
        private var _mediaPlayerSprite:MediaPlayerSprite;


        public function TestPlayer( )
        {
            stage.quality = StageQuality.HIGH;
            initMediaPlayer();
        }

        private function initMediaPlayer():void
        {

            // Create the container (sprite) for managing display and layout
            _mediaPlayerSprite = new MediaPlayerSprite();    
            _mediaPlayerSprite.addEventListener(MediaErrorEvent.MEDIA_ERROR, onPlayerFailed);
            _mediaPlayerSprite.addEventListener(MediaPlayerStateChangeEvent.MEDIA_PLAYER_STATE_CHANGE, onPlayerStateChange);

            //Adds the container to the stage
            addChild(_mediaPlayerSprite);

            // Create a mediafactory instance
            _mediaFactory = new DefaultMediaFactory();

            // Add the listeners for PLUGIN_LOADING
            _mediaFactory.addEventListener(MediaFactoryEvent.PLUGIN_LOAD,onPluginLoaded);
            _mediaFactory.addEventListener(MediaFactoryEvent.PLUGIN_LOAD_ERROR, onPluginLoadFailed );

            // Load the plugin class 
            loadAdaptiveStreamingPlugin( );  

        }

        private function loadAdaptiveStreamingPlugin( ):void
        {
            var pluginResource:MediaResourceBase;
            var adaptiveStreamingPluginUrl:String;

            // Your dynamic plugin web server needs to host a valid crossdomain.xml file to allow loading plugins.

            adaptiveStreamingPluginUrl = "http://yourdomain/MSAdaptiveStreamingPlugin-v1.0.1-osmf2.0.swf";
            pluginResource = new URLResource(adaptiveStreamingPluginUrl);
            _mediaFactory.loadPlugin( pluginResource ); 

        }

        private function onPluginLoaded( event:MediaFactoryEvent ):void
        {
            // The plugin is loaded successfully.

            // Your web server needs to host a valid crossdomain.xml file to allow plugin to download Smooth Streaming files.

    loadMediaSource("http://devplatem.vo.msecnd.net/Sintel/Sintel_H264.ism/manifest")
        }

        private function onPluginLoadFailed( event:MediaFactoryEvent ):void
        {
            // The plugin is failed to load ...
        }


        private function onPlayerStateChange(event:MediaPlayerStateChangeEvent) : void
        {
            var state:String;

            state =  event.state;

            switch (state)
            {
                case MediaPlayerState.LOADING: 

                    // A new source is started to load.

                    break;

                case  MediaPlayerState.READY :   
                    // Add code to deal with Player Ready when it is hit the first load after a source is loaded. 

                    break;

                case MediaPlayerState.BUFFERING :

                    break;

                case  MediaPlayerState.PAUSED :
                    break;      
                // other states ...          
            }
        }

        private function onPlayerFailed(event:MediaErrorEvent) : void
        {
            // Media Player is failed .           
        }

        private function loadMediaSource(sourceURL : String):void 
        {
            // Take an URL of SmoothStreamingSource’s manifest and add it to the page.

            var resource:URLResource= new URLResource( sourceURL );

            var element:MediaElement = _mediaFactory.createMediaElement( resource );
            _mediaPlayerSprite.scaleMode = ScaleMode.LETTERBOX;
            _mediaPlayerSprite.width = stage.stageWidth;
            _mediaPlayerSprite.height = stage.stageHeight;
            // Add the media element
            _mediaPlayerSprite.media = element;
        }     

    }
}
</code></pre>

<h2>Strobe Media  Playback with the SS ODMF Dynamic Plugin</h2>

<p>The Smooth Streaming for OSMF dynamic plugin is compatible with <a href="http://www.osmf.org/strobe_mediaplayback.html">Strobe Media Playback (SMP)</a>. You can use the SS for OSMF plugin to add Smooth Streaming content playback to SMP. To do this, copy “MSAdaptiveStreamingPlugin-v1.0.1-osmf2.0.swf” under a web server for HTTP load using the following steps:</p>

<ol>
<li>Browse the <a href="http://osmf.org/dev/2.0gm/setup.html">Strobe Media Playback setup page</a>. </li>
<li>Set the src to a Smooth Streaming source, (e.g. http://devplatem.vo.msecnd.net/Sintel/Sintel_H264.ism/manifest) </li>
<li><p>Make the desired configuration changes and click Preview and Update.</p>

<p><strong>Note</strong> Your content web server needs a valid crossdomain.xml. </p></li>
<li><p>Copy the preview code and paste the code to a simple HTML page using your favorite text editor, such as in the following example:</p>

<pre><code>&lt;html&gt;
&lt;body&gt;
&lt;object width="920" height="640"&gt; 
&lt;param name="movie" value="http://osmf.org/dev/2.0gm/StrobeMediaPlayback.swf"&gt;&lt;/param&gt;
&lt;param name="flashvars" value="src=http://devplatem.vo.msecnd.net/Sintel/Sintel_H264.ism/manifest &amp;autoPlay=true"&gt;&lt;/param&gt;
&lt;param name="allowFullScreen" value="true"&gt;&lt;/param&gt;
&lt;param name="allowscriptaccess" value="always"&gt;&lt;/param&gt;
&lt;param name="wmode" value="direct"&gt;&lt;/param&gt;
&lt;embed src="http://osmf.org/dev/2.0gm/StrobeMediaPlayback.swf" 
    type="application/x-shockwave-flash" 
    allowscriptaccess="always" 
    allowfullscreen="true" 
    wmode="direct" 
    width="920" 
    height="640" 
    flashvars=" src=http://devplatem.vo.msecnd.net/Sintel/Sintel_H264.ism/manifest&amp;autoPlay=true"&gt;
&lt;/embed&gt;
&lt;/object&gt;
&lt;/body&gt;
&lt;/html&gt;
</code></pre></li>
<li><p>Add Smooth Streaming OSMF plugin to the embed code and save.</p>

<pre><code>&lt;html&gt;
&lt;object width="920" height="640"&gt; 
&lt;param name="movie" value="http://osmf.org/dev/2.0gm/StrobeMediaPlayback.swf"&gt;&lt;/param&gt;
&lt;param name="flashvars" value="src=http://devplatem.vo.msecnd.net/Sintel/Sintel_H264.ism/manifest&amp;autoPlay=true&amp;plugin_AdaptiveStreamingPlugin=http://yourdomain/MSAdaptiveStreamingPlugin-v1.0.1-osmf2.0.swf&amp;AdaptiveStreamingPlugin_retryLive=true&amp;AdaptiveStreamingPlugin_retryInterval=10"&gt;&lt;/param&gt;
&lt;param name="allowFullScreen" value="true"&gt;&lt;/param&gt;
&lt;param name="allowscriptaccess" value="always"&gt;&lt;/param&gt;
&lt;param name="wmode" value="direct"&gt;&lt;/param&gt;
&lt;embed src="http://osmf.org/dev/2.0gm/StrobeMediaPlayback.swf" 
    type="application/x-shockwave-flash" 
    allowscriptaccess="always" 
    allowfullscreen="true" 
    wmode="direct" 
    width="920" 
    height="640" 
    flashvars="src=http://devplatem.vo.msecnd.net/Sintel/Sintel_H264.ism/manifest&amp;autoPlay=true&amp;plugin_AdaptiveStreamingPlugin=http://yourdomain/MSAdaptiveStreamingPlugin-v1.0.1-osmf2.0.swf&amp;AdaptiveStreamingPlugin_retryLive=true&amp;AdaptiveStreamingPlugin_retryInterval=10"&gt;
&lt;/embed&gt;
&lt;/object&gt;
&lt;/html&gt;
</code></pre></li>
<li><p>Save your HTML page and publish to a web server. Browse to the published web page using your favorite Flash® Player enabled Internet browser (Internet Explorer, Chrome, Firefox, so on).</p></li>
<li>Enjoy Smooth Streaming content inside Adobe® Flash® Player.</li>
</ol>

<p>For more information on general OSMF development, please see the official <a href="http://www.osmf.org/developers.html">OSMF development page</a>.</p>
    </body>
</html>
